rootProject.name = 'SpringModuleX'

def basePackage = packageGroup.replace('.', '/')
def allModules = (noneBootJarModules.split(',') + bootJarModules.split(',')).collect { it.trim() }
allModules.each {
    def compDir = new File(rootDir, it)
    if (!compDir.exists()) {
        compDir.mkdirs()
    }

    compDir.eachDir { subDir ->
        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if (!gradleFile.exists()) {
            gradleFile.text = """
                    dependencies {
                    }
                    """.stripIndent(20)
        }

        // 공통 패키지 경로를 사용하여 src 디렉토리 생성
        [
                "src/main/java/${basePackage}",
                "src/main/resources",
                "src/test/java/${basePackage}",
                "src/test/resources"
        ].each { srcDir ->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if (!srcFolder.exists()) {
                srcFolder.mkdirs()
            }
        }

        def projectName = ":${it}-${subDir.name}"
        include projectName
        project(projectName).projectDir = subDir
    }
}
