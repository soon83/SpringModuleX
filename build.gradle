import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

plugins {
    id 'nu.studer.jooq' version '9.0'
}

allprojects {
    group = packageGroup
    version = '1.0.0-' + new SimpleDateFormat("yyMMdd_HHmmss").format(new Date())

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // default
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        annotationProcessor libs.mapstruct.processor
        implementation libs.spring.boot.starter.web
        implementation libs.spring.boot.starter.validation
        implementation libs.mapstruct

        // test
        testImplementation libs.spring.boot.starter.test
        testRuntimeOnly libs.junit.platform.launcher
        testImplementation libs.rest.assured
    }

    // task
    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.annotationProcessorPath = configurations.annotationProcessor
        }
    }
}


// noneBootJarModules (bootJar = false)
noneBootJarModules.split(',').each { module ->
    def subProjectDir = new File(projectDir, module.trim())
    subProjectDir.eachDir { dir ->
        def projectName = ":${module.trim()}-${dir.name}"
        project(projectName) {
            bootJar.enabled(false)
            jar.enabled(true)
        }
    }
}

// bootJarModules (bootJar = true)
bootJarModules.split(',').each { module ->
    def subProjectDir = new File(projectDir, module.trim())
    subProjectDir.eachDir { dir ->
        def projectName = ":${module.trim()}-${dir.name}"
        project(projectName) {
            jar.enabled(false)
        }
    }
}
